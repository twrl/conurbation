template("efi_boot_driver") {

    dep_target_name = target_name + "_depend"

    executable(target_name) {

        forward_variables_from(invoker, "*")

        output_extension = "efi"

        if (!defined(ldflags)) {
            ldflags = []
        }
        ldflags += [ "/nodefaultlib", "/nxcompat:no", "/entry:efi_main", "/subsystem:efi_boot_service_driver", "/safeseh:no" ]

        if (!defined(cflags)) {
            cflags = []
        }
        cflags += [ "-nostdinc", "-fno-rtti", "-fno-exceptions", "-fno-stack-protector", "-ffreestanding"]

        if (!defined(deps)) {
            deps = []
        }
        deps += [ "//sources/supervisor/kuefi" ]

        if (!defined(public_configs)) {
            public_configs = []
        }
        public_configs += [ ":" + dep_target_name ]

    }

    config(dep_target_name) {

        include_dirs = [ "inc/" ]

    }

}
